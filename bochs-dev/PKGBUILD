# Based on Arch official PKGBUILD

# Contributor: Tom Newsom <Jeepster <at> gmx.co.uk>
# Maintainer: Kevin Piche <kevin <at> archlinux.org>

# Reference: http://wiki.osdev.org/Bochs

pkgname=bochs-dev
_pkgname=bochs
pkgver=2.6.2
pkgrel=1
pkgdesc="A portable x86 PC emulation software package"
arch=('i686' 'x86_64')
url="http://bochs.sourceforge.net/"
license=('LGPL')
depends=('gcc-libs' 'libxrandr')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('82ecaff9826d4f29fa46f3062e2957b8')
provides=('bochs')
conflicts=('bochs')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-wx --enable-cpu-level=6 \
              --enable-fpu --enable-3dnow --enable-disasm \
              --enable-debugger --enable-smp --enable-x86-64 \
              --disable-debugger-gui
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m 644 .bochsrc "${pkgdir}/etc/bochsrc-sample.txt"
}
